Процедура СоздатьПриходныйОрдерНаОснованииПеремещенияТоваров(ПеремещениеТоваров) Экспорт
		
	// PROJECT-666 01.01.2024 vasilevdaal@gmail.com
	
	ДанныеПеремещения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПеремещениеТоваров,
		"СкладПолучатель.ИспользоватьОрдернуюСхемуПриОтгрузке,
		|пр_ЗаявкаНаОтгрузку, пр_ИспользуетсяДоверительнаяПриемка,
		|ХозяйственнаяОперация, СкладОтправитель, СкладПолучатель, Товары,
		|Проведен, Статус, Номер, Дата, ЗаказНаПеремещение");
	ЗаказНаПеремещение = ДанныеПеремещения.ЗаказНаПеремещение;
	ТипСвязи = ПредопределенноеЗначение("Перечисление.пр_ТипыСвязейДокументов.ПеремещениеТоваровПриходныйОрдерНаТовары");
	
	Если ПроверитьУсловиеСозданияПриходногоОрдера(ДанныеПеремещения) Тогда
		
		СвязанныеДокументы = ПолучитьПриходныеОрдераПоПеремещениюТоваров(ПеремещениеТоваров);
		
		ДанныеЗаполнения = Новый Структура(
			"Распоряжение, ХозяйственнаяОперация, Отправитель, Склад, НомерВходящегоДокумента",
			ПеремещениеТоваров,
			ДанныеПеремещения.ХозяйственнаяОперация,
			ДанныеПеремещения.СкладОтправитель,
			ДанныеПеремещения.СкладПолучатель,
			ДанныеПеремещения.Номер
		);
			
		ДанныеЗаполнения.Вставить("пр_ЗаявкаНаОтгрузку", ДанныеПеремещения.пр_ЗаявкаНаОтгрузку);
		
		РезультатПолученияДокумента = ПолучитьДокументОбъектПоСвязаннымДокументам(
			ДанныеЗаполнения, СвязанныеДокументы, ТипСвязи);
		ДокументОбъект = РезультатПолученияДокумента.ДокументОбъект;
		ДокументОбъект.Заполнить(ДанныеЗаполнения);
		
		ДокументОбъект.пр_ЗаявкаНаОтгрузку = ДанныеПеремещения.пр_ЗаявкаНаОтгрузку;
		ДокументОбъект.пр_ИспользуетсяДоверительнаяПриемка = ДанныеПеремещения.пр_ИспользуетсяДоверительнаяПриемка;

		
		Если ДокументОбъект.пр_ИспользуетсяДоверительнаяПриемка Тогда
			ЗаполнитьТоварыПоДоверительнойПриемке(ДокументОбъект, ПеремещениеТоваров);
		Иначе
			ДокументОбъект.ЗаполнитьТоварыПоТоварамКПоступлению("НоменклатураКоличество", ТекущаяДатаСеанса());
		КонецЕсли;
		
		Если ДокументОбъект.ПроверитьЗаполнение()
			И ДокументОбъект.Товары.Количество() > 0 Тогда
			Попытка
				ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
				ЗаписатьСвязьДокументов(ПеремещениеТоваров, ДокументОбъект.Ссылка, ТипСвязи);
			Исключение
				Шаблон = НСтр("ru='Не удалось сформировать документ ""Приходный ордер на товары"" по перемещению %1 от 2 по причине %3'");
				ТекстОшибки = СтрШаблон(Шаблон, 
					ДанныеПеремещения.Номер,
					ДанныеПеремещения.Дата,
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				ВызватьИсключение ТекстОшибки;
			КонецПопытки;
		Иначе
			ТекстСообщенийПользователю = пр_ОбщегоНазначения.ТекстСообщенийПользователю(, Истина);
			ВызватьИсключение ТекстСообщенийПользователю;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
